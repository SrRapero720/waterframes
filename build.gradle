buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url = 'https://repo.spongepowered.org/maven' }
        maven { url = 'https://maven.minecraftforge.net' }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
    }
}


apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

/*
1. | Major update (rewrites or fml version changes)
0. | Minor Update (new functions)
0- | Minor features (new language added)
A/B/C/D | Patches or Hotfixes
* */
version = '1.0.0-A'
group= "me.srrapero720" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
java.toolchain.languageVersion = JavaLanguageVersion.of(17)


configurations {
    library
    implementation.extendsFrom library
}

minecraft {
    mappings channel: 'official', version: '1.18.2'

    runs {
        client {
            taskName "waterframes_client"
            workingDirectory project.file('run')
			
			property 'forge.logging.console.level', 'debug'
			arg '--mixin.config'
			arg 'waterframes.mixin.json'
        }

        server {
            taskName "waterframes_server"
            workingDirectory project.file('run')

            property 'forge.logging.console.level', 'debug'
            arg '--mixin.config'
            arg 'waterframes.mixin.json'
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url "https://maven.tterrag.com/" }
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.2.0'

    implementation fg.deobf("curse.maven:ferritecore-429235:4074294")
    implementation fg.deobf("curse.maven:fastload-630620:4271605")
    implementation fg.deobf("curse.maven:lazy-dfu-forge-460819:3544496")
    implementation fg.deobf("curse.maven:watercore-782432:4435992")


    implementation fg.deobf("curse.maven:rubidium-574856:4024781")
    implementation fg.deobf("curse.maven:oculus-581495:4354939")

    // Apply Mixin AP
    implementation fg.deobf("curse.maven:creativecore-257814:4394989")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, "watercore.mixin-refmap.json"
}


jar {
    manifest {
        attributes([
            "Specification-Title": project.modname,
            "Specification-Vendor": "SrRapero720",
            "Specification-Version": project.version,
            "Implementation-Title": project.name,
            "Implementation-Version"  : project.version,
            "Implementation-Vendor": "SrRapero720",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
            "TweakOrder": 0,
            "MixinConfigs": "waterframes.mixin.json"
        ])
    }
    from {
        configurations.library.collect {
            it.isDirectory() ? it.matching {
                exclude '*', 'META-INF/**'
            } : zipTree(it).matching {
                exclude '*', 'META-INF/**'
            }
        }
    }
}

jar.finalizedBy('reobfJar')
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}