import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    id 'java'
    id 'idea'
    id 'net.neoforged.gradle.userdev' version '7.0.+'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.1.+'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

base {
    archivesName = modid
    version = "$modloader-mc$mcversion-v$modversion"
}

java.toolchain {
    languageVersion = JavaLanguageVersion.of(21)
}

println "Java: ${System.getProperty 'java.version'}, Gradle Java: ${JavaVersion.current()}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    accessTransformers {
        file('src/main/resources/META-INF/accesstransformer.cfg')
    }
}
runs {
    configureEach {
        systemProperty  'forge.logging.console.level', 'debug'
        systemProperty  'log4j.appender.Console', "org.apache.log4j.ConsoleAppender"
        systemProperty  'terminal.jline', 'true'

        modSource sourceSets.main
    }

    client {
        // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
        systemProperty 'forge.enabledGameTestNamespaces', modid
    }

    server {
        systemProperty 'forge.enabledGameTestNamespaces', modid
        programArgument '--nogui'
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
    runtimeClasspath.extendsFrom localRuntime
}

repositories {
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://jitpack.io' }
    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup "curse.maven"
        }
    }
    // REMOVE THIS SHIT WHEN GOEDIX RELEASED VIDEOPLAYER ON CURSEFORGE
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    implementation "net.neoforged:neoforge:$fgversion"

    //DEPENDENCIES
    implementation "com.github.WaterMediaTeam.watermedia:build:$watermediaversion"
//    implementation "maven.modrinth:video-player:$videoplayerversion"
    implementation "curse.maven:creativecore-257814:${creativecoreversion}"
}

tasks.processResources.outputs.upToDateWhen { false }
processResources {
    filesMatching(['META-INF/*.toml', 'pack.mcmeta', 'resourcepacks/**/pack.mcmeta', "$modid.$mixin_file_suffix".toString()]) {
        expand project.properties
    }
}

jar {
    manifest {
        attributes "Specification-Title"        : modid
        attributes "Specification-Vendor"       : modauthor
        attributes "Specification-Version"      : version
        attributes "Implementation-Title"       : modname
        attributes "Implementation-Version"     : version
        attributes "Implementation-Vendor"      : modauthor
        attributes "Implementation-Timestamp"   : new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        attributes "MixinConfigs"               : "$modid.$mixin_file_suffix"
    }
    exclude('org/valkyrienskies/**')
}

task upload(type: GradleBuild) {
    enabled = System.getenv("MODRINTH_TOKEN") != null && System.getenv("CURSEFORGE_TOKEN") != null
    description 'Uploads new version to CurseForge and Modrinth'
    group = 'publishing'
    tasks = [ 'curseforge', 'modrinth' ]
}

task curseforge(type: TaskPublishCurseForge) {
    disableVersionDetection()
    apiToken = System.getenv("CURSEFORGE_TOKEN")

    def mainFile = upload(curseforgeid, jar)
    mainFile.displayName = "$modbrandname/${modloader.toLowerCase().capitalize()}/mc$mcversion/v$modversion"
    mainFile.addModLoader("NeoForge") // Neoforge isn't a valid name
    mainFile.addGameVersion(mcversion, "server", "client")
    mainFile.addRequirement("creativecore", "watermedia")
    mainFile.addIncompatibility("stellarity")
    mainFile.changelog = getChangelogText(false)
    mainFile.changelogType = "markdown"
    mainFile.releaseType = versiontype

}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = modrinthid

    uploadFile = jar
    versionName = "$modbrandname/${modloader.toLowerCase().capitalize()}/mc$mcversion/v$modversion"
    versionNumber = version.toString()
    versionType = versiontype
    gameVersions = [ mcversion ]
    changelog = getChangelogText(true)
    loaders = [ "neoforge" ]
    dependencies {
        required.project "creativecore"
        required.project "watermedia"
        incompatible.project "stellarity"
    }
}

String getChangelogText(boolean latest) {
    String result = ''
    for (String line: file('CHANGELOG.md').readLines('UTF-8')) {
        if (latest && line.isEmpty()) return result
        result += line + "\n"
    }
    return result
}